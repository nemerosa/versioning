/**
 * Plug-in development
 */

plugins {
    /**
     * Build performance
     */
    id 'com.gradle.build-scan' version '1.16'
    /**
     * Plugin
     */
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    /**
     * Versioning applied to itself
     */
    id 'net.nemerosa.versioning' version '2.7.1'
    /**
     * Written in Groovy
     */
    id 'groovy'
}

buildScan {
    termsOfServiceUrl   = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

/**
 * Meta information
 */

group = 'net.nemerosa'
version = versioning.info.display

/**
 * Dependencies
 */

ext {
    jgitVersion = '5.2.1.201812262042-r'
}

repositories {
   jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar.grgit:grgit-core:3.0.0'
    compile "org.eclipse.jgit:org.eclipse.jgit.ui:${jgitVersion}"
    compile "org.eclipse.jgit:org.eclipse.jgit:${jgitVersion}"
    compile 'org.tmatesoft.svnkit:svnkit:1.9.3'
    testCompile "junit:junit:4.12"
    testCompile "commons-lang:commons-lang:2.6"
    testCompile "commons-io:commons-io:2.6"
}

/**
 * Plug-in definition
 */

pluginBundle {
    website = 'https://github.com/nemerosa/versioning/'
    vcsUrl = 'https://github.com/nemerosa/versioning/'
    tags = ['gradle', 'plugin', 'scm', 'git', 'svn', 'version']
}

gradlePlugin {
    plugins {
        versioningPlugin {
            id = 'net.nemerosa.versioning'
            displayName = 'Versioning plugin for Gradle'
            description = 'Gradle plug-in that computes version information from the SCM'
            implementationClass = 'net.nemerosa.versioning.VersioningPlugin'
        }
    }
}

test {
  environment "GIT_TEST_BRANCH", "feature/456-cute"
  environment "SVN_TEST_BRANCH", "feature-456-cute"
}
